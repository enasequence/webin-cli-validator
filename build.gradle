plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.20.0"
}

group = 'uk.ac.ebi.ena.webin-cli'
version = '2.0.1'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/projects/1414/packages/maven"
        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = project.ext.properties.gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

dependencies {
    implementation( 'org.springframework:spring-web:6.1.13' )
    implementation( 'org.springframework.retry:spring-retry:1.3.4' )

    implementation('com.fasterxml.jackson.core:jackson-databind:2.17.2')

    implementation('org.slf4j:slf4j-api:2.0.7')

    testImplementation('junit:junit:4.12')
    testImplementation('org.assertj:assertj-core:3.11.1')

    testRuntimeOnly('ch.qos.logback:logback-classic:1.3.14')
}

test {
    // Webin account
    if( System.env.webin_password ) {
        environment 'webin-password', System.env.webin_password
    } else {
        environment 'webin-password', webinPassword
    }

    if( System.env.webin_username ) {
        environment 'webin-username', System.env.webin_username
    } else {
        environment 'webin-username', webinUsername
    }

    // Biosamples Webin account
    if( System.env.biosamples_webin_password ) {
        environment 'biosamples-webin-password', System.env.biosamples_webin_password
    } else {
        environment 'biosamples-webin-password', biosamplesWebinPassword
    }

    if( System.env.biosamples_webin_username ) {
        environment 'biosamples-webin-username', System.env.biosamples_webin_username
    } else {
        environment 'biosamples-webin-username', biosamplesWebinUsername
    }
}

spotless {
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        licenseHeader('''/*
 * Copyright 2018-2023 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
''')
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
        googleJavaFormat()
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/1414/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
